import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.bristermitten.me/repository/maven-public/'
    }
}
configurations {
    unzipme { transitive = false }
    compileOnly { extendsFrom unzipme }
}

dependencies {
    compileOnly gradleApi()
    unzipme project(':common-lib')
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compile 'com.google.code.gson:gson:2.8.6'
    compile 'com.google.guava:guava:21.0'
}

processResources {
    from zipTree(configurations.unzipme.singleFile)
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

pluginBundle {
    website = 'https://github.com/knightzmc/pdm'
    vcsUrl = 'https://github.com/knightzmc/pdm'
    tags = ['spigot', 'shading', 'runtime', 'bukkit', 'plugin']
    withDependencies {
        it.clear()
    }
}

gradlePlugin {
    plugins {
        pdm {
            id = 'me.bristermitten.pdm'
            displayName = 'Plugin Dependency Manager'
            description = 'Sets up PDM to automatically download any dependencies for Spigot plugins'
            implementationClass = 'me.bristermitten.pdm.PDM'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives sourcesJar
}
