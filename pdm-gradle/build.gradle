import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'com.gradle.plugin-publish' version '0.12.0'
}


repositories {
    mavenCentral()
}

dependencies {
    implementation project(':pdm')
    compileOnly gradleApi()
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.guava:guava:21.0'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    minimize()
    classifier = ''
}


pluginBundle {
    website = 'https://github.com/knightzmc/pdm'
    vcsUrl = 'https://github.com/knightzmc/pdm'
    tags = ['spigot', 'shading', 'runtime', 'bukkit', 'plugin']
}

gradlePlugin {
    plugins {
        pdm {
            id = 'me.bristermitten.pdm'
            displayName = 'Plugin Dependency Manager'
            description = 'Sets up PDM to automatically download any dependencies for Spigot plugins'
            implementationClass = 'me.bristermitten.pdm.PDM'
        }
    }
}

artifacts {
    add("archives", shadowJar)
}
