import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.bristermitten.me/repository/maven-public/'
    }
}

configurations {
    unzip { transitive = false }
    shade
    compileOnly {
        extendsFrom unzip
        extendsFrom shade
    }
    testImplementation {
        extendsFrom shade
    }
}

dependencies {
    unzip project(':common-lib')
    shade 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72'
    shade 'com.google.code.gson:gson:2.8.6'
    shade 'com.google.guava:guava:21.0'

}

jar {
    from {
        configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    from zipTree(configurations.unzip.singleFile)
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

gradlePlugin {
    plugins {
        pdm {
            id = 'me.bristermitten.pdm'
            implementationClass = 'me.bristermitten.pdm.PDM'
        }
    }
}

pluginBundle {
    website = 'https://github.com/knightzmc/pdm'
    vcsUrl = 'https://github.com/knightzmc/pdm'
    tags = ['spigot', 'shading', 'runtime', 'bukkit', 'plugin']
    description = 'Sets up PDM to automatically download any dependencies for Spigot plugins'

    plugins {
        pdm {
            displayName = 'Plugin Dependency Manager'
        }
    }
}

artifacts {
    archives sourcesJar
}
